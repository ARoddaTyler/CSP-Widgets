package Libraries.Curriculum.Code

use Libraries.Interface.Item2D
use Libraries.Game.Graphics.Drawable
use Libraries.Game.Graphics.Color
use Libraries.Game.GameStateManager
use Libraries.Game.Graphics.Label

class FocusHighlight is Item2D

    Drawable leftBorder
    Drawable rightBorder
    Drawable bottomBorder
    Drawable topBorder

    Item2D focus = undefined
    number thickness = 5

    on create
        SetZ(-10)

        leftBorder:LoadFilledRectangle(10, 10)
        rightBorder:LoadFilledRectangle(10, 10)
        bottomBorder:LoadFilledRectangle(10, 10)
        topBorder:LoadFilledRectangle(10, 10)

        leftBorder:SetAcceptsMouseInput(false)
        rightBorder:SetAcceptsMouseInput(false)
        bottomBorder:SetAcceptsMouseInput(false)
        topBorder:SetAcceptsMouseInput(false)
        SetAcceptsMouseInput(false)

        Add(leftBorder)
        Add(rightBorder)
        Add(bottomBorder)
        Add(topBorder)
    end

    action SetColor(Color color)
        leftBorder:SetColor(color)
        rightBorder:SetColor(color)
        bottomBorder:SetColor(color)
        topBorder:SetColor(color)
    end

    action SetThickness(number thickness)
        me:thickness = thickness

        if focus not= undefined
            SetFocus(focus)
        end
    end

    action SetFocus(Item2D item)
        focus = item
        SetPosition(item:GetX(), item:GetY())
        SetSize(item:GetWidth(), item:GetHeight())

        number labelOffset = 0
        if item is Label
            Label label = cast(Label, item)
            labelOffset = label:GetHeight() - label:GetLineHeight()
        end

        leftBorder:SetPosition(-thickness, -thickness - labelOffset)
        leftBorder:SetSize(thickness, GetHeight() + thickness * 2)

        rightBorder:SetPosition(GetWidth(), -thickness - labelOffset)
        rightBorder:SetSize(thickness, GetHeight() + thickness * 2)

        bottomBorder:SetPosition(0, -thickness - labelOffset)
        bottomBorder:SetSize(GetWidth(), thickness)

        topBorder:SetPosition(0, GetHeight() - labelOffset)
        topBorder:SetSize(GetWidth(), thickness)
    end

    action Update(number seconds)
        if focus not= undefined
            if focus:GetWidth() not= GetWidth() or focus:GetHeight() not= GetHeight() or focus:GetX() not= GetX() or focus:GetY() not= GetY()
                SetFocus(focus)
            end
        end
    end

end