use Libraries.Compute.Statistics.DataFrameTransform
use Libraries.Compute.Statistics.DataFrame
use Libraries.Compute.Statistics.DataFrameColumn
use Libraries.Compute.Statistics.DataFrameSelection

/*
    This class provides a filter for Data Frames.
*/
class Filter is DataFrameTransform
    text filter = undefined
    integer column = -1

    action Transform(DataFrame frame) returns DataFrame 
        if filter = undefined
            return undefined
        end

        DataFrame newFrame = frame:Copy(0, frame:GetSize(), 0, 0)

        DataFrameSelection selection = frame:GetSelection()
        integer col = 0
        repeat while col < selection:GetColumnSize()
            newFrame:AddSelectedColumn(selection:GetColumn(col))
            col = col + 1
        end
        col = 0
        repeat while col < selection:GetFactorSize()
            newFrame:AddSelectedFactor(selection:GetFactor(col))
            col = col + 1
        end
        DataFrameColumn filterColumn = frame:GetColumn(column)
        integer i = 0
        repeat while i < filterColumn:GetSize()
            if not filterColumn:IsUndefined(i)
                text value = filterColumn:GetAsText(i)
                if filter = value //copy this row
                    CopyRow(frame, newFrame, i)
                end
            end
            i = i + 1
        end
        return newFrame
    end
    
    private action CopyRow(DataFrame from, DataFrame to, integer index)
        i = 0
        repeat while i < from:GetSize()
            DataFrameColumn fromColumn = from:GetColumn(i)
            if index < fromColumn:GetSize()
                DataFrameColumn toColumn = to:GetColumn(i)
                toColumn:Add(fromColumn:GetAsText(index))
            end
            
            i = i + 1
        end
    end

    action GetFilter returns text
        return filter
    end

    action SetFilter(text filter)
        me:filter = filter
    end

    action GetColumn returns integer
        return column
    end

    action SetColumn(integer column)
        me:column = column
    end
end