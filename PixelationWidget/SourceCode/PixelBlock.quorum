use Libraries.Game.Graphics.Drawable
use Libraries.Game.Graphics.Color
use Libraries.Interface.Events.FocusEvent

class PixelBlock is Drawable

    Drawable highlight

    on create
        SetAccessibilityCode(parent:Item:ITEM)
        SetFocusable(true)
        SetInputGroup("PixelBlock")

        SetZ(-1)

        Color color
        color:SetColor(1, 1, 0.5, 1)
        highlight:LoadFilledRectangle(1, 1, color)
        highlight:SetZ(0.5)
        highlight:Hide()
        Add(highlight)
    end

    action GetNextFocus returns Item
        return GetRegion():GetNextFocus()
    end

    action GetPreviousFocus returns Item
        return GetRegion():GetPreviousFocus()
    end

    action GetRegion returns PixelRegion
        return cast(PixelRegion, GetParent())
    end

    action GainedFocus(FocusEvent event)
        integer padding = GetRegion():GetPixelPadding()
        highlight:SetPosition(-padding, -padding)
        highlight:SetSize(GetWidth() + 2 * padding, GetHeight() + 2 * padding)
        highlight:Show()
    end

    action LostFocus(FocusEvent event)
        highlight:Hide()
    end
end