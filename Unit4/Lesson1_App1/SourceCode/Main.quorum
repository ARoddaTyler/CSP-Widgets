use Libraries.Curriculum.Code.all
use Libraries.Interface.Controls.Button
use Libraries.Interface.Controls.Icon
use Libraries.Game.Graphics.Label

//START HERE
//Explore the app and write down at least two examples where the app seems to be keeping track of a piece of information or using it to make decisions.

class Main is Application

    integer clicks = 0
    integer clickPower = 1

    action Start

        ShowFocusHighlight(true)
        SetFocusHighlightColor(NewColor(0, 0, 1))
        SetFocusHighlightThickness(4)

        Page page
        page:SetName("Home Page")

        Label directions = CreateLabel()
        directions:SetName("Directions")
        directions:SetText("This pet rock needs some love. Hover over the rock and click the rock or press the space bar to pet him to make him happy! If you pet him enough times, he just might evolve into a very special pet rock. Use the buttons at the bottom to upgrade your selects so that each select is worth more!")               
        directions:SetPixelX(25)
        directions:SetPixelY(560)
        directions:SetMaximumWidth(350)
        directions:SetFontSize(19)
        directions:SetColor(NewColor(0, 0.58, 0.79))
        page:Add(directions)

        Button level1 = CreateButton("Pet Rock Level 1.jpg")
        level1:SetName("Pet Rock Level 1: Rock-a-Bye")
        level1:SetDescription("The rock currently looks normal. You are now hovering over the rock and can select him to make him happy! You can check how many times you have selected him by checking the select counter below.")
        level1:SetPixelWidth(132)
        level1:SetPixelHeight(100)
        level1:SetPixelX(134)
        level1:SetPixelY(250)
        page:Add(level1)

        Button level2 = CreateButton("Pet Rock Level 2.jpg")
        level2:SetName("Pet Rock Level 2: Rock-a-Bunny")
        level2:SetDescription("The rock received enough love to level up! The rock now has bunny ears and is happy. Select the rock more times to make him even happier!")
        level2:SetPixelWidth(132)
        level2:SetPixelHeight(174)
        level2:SetPixelX(134)
        level2:SetPixelY(250)
        level2:Hide()
        page:Add(level2)

        Button level3 = CreateButton("Pet Rock Level 3.jpg")
        level3:SetName("Pet Rock Level 3: Rock-a-Walk-a-Rainbow")
        level3:SetDescription("The rock received enough love to level up into its happiest form! The rock now is now very happy and has colorful bunny ears and sees a rainbow. EVOLUTION COMPLETE")
        level3:SetPixelWidth(200)
        level3:SetPixelHeight(200)
        level3:SetPixelX(100)
        level3:SetPixelY(225)
        level3:Hide()
        page:Add(level3)

        Label descriptor = CreateLabel()
        descriptor:SetFocusable(false)
        descriptor:SetName("Descriptor")
        descriptor:SetText("Level 1: Rock-a-Bye")
        descriptor:SetFontSize(20)
        descriptor:SetColor(NewColor(0, 0.58, 0.79))
        descriptor:SetPixelX(100)
        descriptor:SetPixelY(200)
        page:Add(descriptor)

        Label clickLabel = CreateLabel()
        clickLabel:SetName("Selects")
        clickLabel:SetText("Selects: 0")
        clickLabel:SetDescription("0")
        clickLabel:SetFontSize(20)
        clickLabel:SetColor(NewColor(0, 0.58, 0.79))
        clickLabel:SetPixelX(40)
        clickLabel:SetPixelY(130)
        page:Add(clickLabel)

        Label clickPowerLabel = CreateLabel()
        clickPowerLabel:SetName("Select Power")
        clickPowerLabel:SetText("+1")
        clickPowerLabel:SetDescription("Each click or select on the space bar is currently worth +1 select")
        clickPowerLabel:SetFontSize(20)
        clickPowerLabel:SetColor(NewColor(0, 0.58, 0.79))
        clickPowerLabel:SetPixelX(320)
        clickPowerLabel:SetPixelY(130)
        page:Add(clickPowerLabel)

        Icon banner = CreateRectangle(NewColor(0, 0.58, 0.79))
        banner:SetFocusable(false)
        banner:SetPixelWidth(400)
        banner:SetPixelHeight(40)
        banner:SetPixelX(0)
        banner:SetPixelY(80)
        page:Add(banner)
        
        Label upgradeLabel = CreateLabel()
        upgradeLabel:SetName("Banner")
        upgradeLabel:SetText("Upgrade Selector")
        upgradeLabel:SetFontSize(20)
        upgradeLabel:SetColor(White())
        upgradeLabel:SetPixelX(125)
        upgradeLabel:SetPixelY(90)
        page:Add(upgradeLabel)

        Button upgrade1 = CreateButton()
        upgrade1:SetName("+2 (cost = 5 selects)")
        upgrade1:SetDescription("You must have 5 selects to buy this upgrade. Select this button to make each 1 click or select count as +2 selects.")
        upgrade1:SetFontSize(16)
        upgrade1:SetPixelX(25)
        upgrade1:SetPixelY(20)
        upgrade1:SetPixelWidth(150)
        upgrade1:SetPixelHeight(40)
        page:Add(upgrade1)

        Button upgrade2 = CreateButton()
        upgrade2:SetName("+20 (cost = 20 selects)")
        upgrade2:SetDescription("You must have 20 selects to buy this upgrade. Select this button to make each 1 click or select count as +20 selects.")
        upgrade2:SetFontSize(16)
        upgrade2:SetPixelX(200)
        upgrade2:SetPixelY(20)
        upgrade2:SetPixelWidth(175)
        upgrade2:SetPixelHeight(40)
        page:Add(upgrade2)

        AddPage(page)
    end

    action ButtonActivated(Button button)
        text name = button:GetName()

        // The following code is for if user clicked on the pet rock
        if name:Contains("Pet Rock")
            clicks = clicks + clickPower

            Page page = GetCurrentPage()
            Label clicksLabel = page:GetLabel("Selects")
            clicksLabel:SetText("Selects: " + clicks)
            clicksLabel:SetDescription("" + clicks)
            
            // This nested if logic is for if we're ready to go from level 1 to 2
            if clicks >= 10 and name:Contains("Level 1")
                button:Hide()

                Button level2 = page:GetButton("Pet Rock Level 2: Rock-a-Bunny")
                level2:Show()
                level2:Focus()

                Label descriptor = page:GetLabel("Descriptor")
                descriptor:SetText("Pet Rock Level 2: Rock-a-Bunny")
                
                PlaySound("vibrant_game_positive_item_2.ogg")
            end

            // This nested if logic is for if we're ready to go from level 2 to 3
            if clicks >= 100 and name:Contains("Level 2")
                button:Hide()

                Button level2 = page:GetButton("Pet Rock Level 3: Rock-a-Walk-a-Rainbow")
                level2:Show()
                level2:Focus()

                Label descriptor = page:GetLabel("Descriptor")
                descriptor:SetText("Pet Rock Level 3: Rock-a-Walk-a-Rainbow
EVOLUTION COMPLETE")
                descriptor:SetPixelX(10)
                
                PlaySound("vibrant_game_positive_item_2.ogg")
            end
        end


        // This nested if logic is for if user clicked the first upgrade
        if name = "+2 (cost = 5 selects)" and clicks >= 5
            clicks = clicks - 5
            clickPower = 2
            
            Page page = GetCurrentPage()
            Label clicksLabel = page:GetLabel("Selects")
            clicksLabel:SetText("Selects: " + clicks)
            clicksLabel:SetDescription("" + clicks)

            Label powerLabel = page:GetLabel("Select Power")
            powerLabel:SetText("+2")
            powerLabel:SetDescription("Now each click or select will be worth +2")
            powerLabel:Focus()

            button:Hide()

            PlaySound("hollow_bell_notification.ogg")
        end

        // This nested if logic is for if user clicked the second upgrade
        if name = "+20 (cost = 20 selects)" and clicks >= 20
            clicks = clicks - 20
            clickPower = 20
            
            Page page = GetCurrentPage()
            Label clicksLabel = page:GetLabel("Selects")
            clicksLabel:SetText("Selects: " + clicks)
            clicksLabel:SetDescription("" + clicks)

            Label powerLabel = page:GetLabel("Select Power")
            powerLabel:SetText("+20")
            powerLabel:SetDescription("Now each click or select will be worth +20")
            powerLabel:Focus()

            Button upgrade1 = page:GetButton("+2 (cost = 5 selects)")
            upgrade1:Hide()

            button:Hide()

            PlaySound("hollow_bell_notification.ogg")
        end
    end

    action Main
        StartGame()
    end

end