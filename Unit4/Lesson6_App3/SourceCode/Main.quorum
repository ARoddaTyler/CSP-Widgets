use Libraries.Curriculum.Code.all
use Libraries.Game.Graphics.Label
use Libraries.Interface.Controls.Icon
use Libraries.Interface.Controls.TextBox
use Libraries.Interface.Controls.Button

class Main is Application
    /*START HERE
    DO THIS:
    * Play the game at least once.
    * Discuss with a partner what's changed since last time in how the game works.
    * Find the if-elseif statement that was added.
    * Draw a flowchart for the if-elseif statement that was added.
    * Modify the app to add one more username and password. You'll need to edit both the code and
        user interface. WARNING: Use a fake password! It's never a good idea to use real passwords
        directly in your code like this.

    Here are the default usernames and passwords:
    Lemonhead -- lemon123
    Mellow Yellow -- iLoveLemons
    */

    // The next 2 lines create and assign variables
    integer score = 0
    integer lives = 3

    // The next 2 lines create variables for the pages of the application.
    // This will let us easily swap between them later.
    Page homePage
    Page gamePage

    // The next 4 lines create variables for a few of the elements in the app so we can easily modify them later.
    // These start undefined (or empty) and we assign values to them in "Start".
    TextBox instructions = undefined
    Button lemon = undefined
    Button lime = undefined
    Label scoreLabel = undefined

    // These next 2 lines create variables that will be used for username and password but start empty.
    TextBox userNameInput = undefined
    TextBox passwordInput = undefined

    action Start

        // The next three lines allow a box to surround the item that is currently in focus.
        ShowFocusHighlight(true)
        SetFocusHighlightColor(NewColor(0, 0, 1))
        SetFocusHighlightThickness(4)

        // The next line sets the background color for the app
        SetBackgroundColor(NewColor(0.59, 0.76, 0.34))

        // The following code adds the elements for the home page.

        Icon banner = CreateRectangle(NewColor(0.41, 0.53, 0.24))
        banner:SetFocusable(false)
        banner:SetPixelX(0)
        banner:SetPixelY(530)
        banner:SetPixelWidth(400)
        banner:SetPixelHeight(70)
        homePage:Add(banner)

        Label title = CreateLabel()
        title:SetName("Title")
        title:SetText("Lemon Squeeze")
        title:SetColor(NewColor(0.97, 0.93, 0.38))
        title:SetPixelX(20)
        title:SetPixelY(550)
        title:SetSize(40)
        homePage:Add(title)

        Icon lemonIcon = CreateIcon("Lemon Icon.png")
        lemonIcon:SetFocusable(false)
        lemonIcon:SetPixelWidth(70)
        lemonIcon:SetPixelHeight(70)
        lemonIcon:SetPixelX(330)
        lemonIcon:SetPixelY(530)
        homePage:Add(lemonIcon)

        instructions = CreateTextBox()
        instructions:SetPixelX(50)
        instructions:SetPixelY(300)
        instructions:SetPixelWidth(300)
        instructions:SetPixelHeight(200)
        instructions:SetName("Instructions")
        instructions:SetText("Welcome Lemon Lovers!
Collect lemons!
Avoid limes!")
        instructions:SetFontSize(20)
        homePage:Add(instructions)        

        Label userNameLabel = CreateLabel()
        userNameLabel:SetFocusable(false)
        userNameLabel:SetText("User Name:")
        userNameLabel:SetPixelX(25)
        userNameLabel:SetPixelY(245)
        userNameLabel:SetFontSize(20)
        homePage:Add(userNameLabel)        

        userNameInput = CreateTextBox()
        userNameInput:SetName("User Name")
        userNameInput:SetPixelX(150)
        userNameInput:SetPixelY(240)
        userNameInput:SetPixelWidth(200)
        userNameInput:SetPixelHeight(30)
        userNameInput:SetFontSize(20)
        homePage:Add(userNameInput)

        Label passwordLabel = CreateLabel()
        passwordLabel:SetFocusable(false)
        passwordLabel:SetText("Password:")
        passwordLabel:SetPixelX(25)
        passwordLabel:SetPixelY(210)
        passwordLabel:SetFontSize(20)
        homePage:Add(passwordLabel)        

        passwordInput = CreateTextBox()
        passwordInput:SetName("Password")
        passwordInput:SetPixelX(150)
        passwordInput:SetPixelY(200)
        passwordInput:SetPixelWidth(200)
        passwordInput:SetPixelHeight(30)
        passwordInput:SetFontSize(20)
        homePage:Add(passwordInput)

        Button playButton = CreateButton()
        playButton:SetName("Play!")
        playButton:SetPixelWidth(120)
        playButton:SetPixelHeight(80)
        playButton:SetFontSize(45)
        playButton:SetPixelX(140)
        playButton:SetPixelY(60)
        playButton:SetBackgroundColor(NewColor(0.97, 0.93, 0.38))
        homePage:Add(playButton)

        AddPage(homePage)

        // The following code adds the elements to the game page.
        lime = CreateButton("Lime.png")
        lime:SetName("Lime")
        lime:SetPixelWidth(200)
        lime:SetPixelHeight(200)
        gamePage:Add(lime)

        lemon = CreateButton("Lemon.png")
        lemon:SetName("Lemon")
        lemon:SetPixelWidth(200)
        lemon:SetPixelHeight(200)
        gamePage:Add(lemon)

        Icon banner2 = CreateRectangle(NewColor(0.41, 0.53, 0.24))
        banner2:SetFocusable(false)
        banner2:SetPixelX(0)
        banner2:SetPixelY(0)
        banner2:SetPixelWidth(400)
        banner2:SetPixelHeight(50)
        gamePage:Add(banner2)

        scoreLabel = CreateLabel()
        scoreLabel:SetName("Score")
        scoreLabel:SetColor(NewColor(0.97, 0.93, 0.38))
        scoreLabel:SetPixelX(20)
        scoreLabel:SetPixelY(10)
        scoreLabel:SetSize(25)
        gamePage:Add(scoreLabel)

        AddPage(gamePage)
    end

    action ButtonActivated(Button button)
        text name = button:GetName()

        // This code is run when the start button is selected
        if name = "Play!"

            // The next 8 lines set starting values for variables and properties.
            score = 0
            lives = 3

            scoreLabel:SetText("Score: " + score + " Lives: " + lives)
            scoreLabel:SetDescription(score + " Lives: " + lives)

            lime:SetPixelX(RandomNumber(0, 200))
            lime:SetPixelY(RandomNumber(50, 400))
            lemon:SetPixelX(RandomNumber(0, 200))
            lemon:SetPixelY(RandomNumber(50, 400))

            // The next 2 lines get the current values stored in username and password variables
            userName = userNameInput:GetText()
            password = passwordInput:GetText()
            
            // The following code checks login information and if the username and password match, the code below the if statement is run
            if userName = "Lemonhead" and password = "lemon123"
                PlaySound("vibrant_game_cartoon_start_game_2_long.ogg")
                ShowPage(gamePage)
            elseif userName = "Mellow Yellow" and password = "iLoveLemons"
                PlaySound("vibrant_game_cartoon_start_game_2_long.ogg")
                ShowPage(gamePage)
            else
                PlaySound("harp_downscale_1.ogg")
            end
        end

        // Event handler for when the lemon is selected
        if name = "Lemon"
            PlaySound("collect_item_bling_1.ogg")

            // The next line increases the score
            score = score + 1

            // The following code sets the size of the lemon based on the score.
            if score > 10
                lemon:SetPixelWidth(100)
                lemon:SetPixelHeight(100)
            elseif score > 5
                lemon:SetPixelWidth(150)
                lemon:SetPixelHeight(150)
            else
                lemon:SetPixelWidth(200)
                lemon:SetPixelHeight(200)
            end

            // The following code moves the lemon and lime to random locations
            lime:SetPixelX(RandomNumber(0, 200))
            lime:SetPixelY(RandomNumber(50, 400))
            lemon:SetPixelX(RandomNumber(0, 200))
            lemon:SetPixelY(RandomNumber(50, 400))

            // The next 2 lines update screen text
            scoreLabel:SetText("Score: " + score + " Lives: " + lives)
            scoreLabel:SetDescription(score + " Lives: " + lives)
        end

        // Event handler for when the lime is selected
        if name = "Lime"
            PlaySound("retro_game_enemy_spawn_7.ogg")

            // The next line of code decreases lives
            lives = lives - 1

            // The following code moves the lemon and lime to random locations
            lime:SetPixelX(RandomNumber(0, 200))
            lime:SetPixelY(RandomNumber(50, 400))
            lemon:SetPixelX(RandomNumber(0, 200))
            lemon:SetPixelY(RandomNumber(50, 400))

            // The next 2 lines update screen text
            scoreLabel:SetText("Score: " + score + " Lives: " + lives)
            scoreLabel:SetDescription(score + " Lives: " + lives)

            // The following code returns players to the homescreen when the lives variable is less than 0
            if lives < 0
                instructions:SetText("You collected " + score + " lemons!
Play again?")
                ShowPage(homePage)
            end
        end

    end

    action Main
        StartGame()
    end

end